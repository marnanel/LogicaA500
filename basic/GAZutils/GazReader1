   10 REM > GazReader1
   20 REM basic program to display areal unit names
   30 REM Paul Cunnell (Logica) 18.9.87
   40:
   50 recsize% = 48              :REM gazetteer record size (bytes)
   60 maxtypes% = 22             :REM limit for types to display
   70 hide_invalid_names% = TRUE :REM don't display names which have no
   80                            :REM valid MAPDATA record
   90 data_file$ = ":4.ads.r.Gazetteer" :REM use '-vfs-gazetteer' for vfs
  100 DIM rec% recsize%
  110:
  120 ON ERROR PROCtidy_up : REPORT:PRINT" @ ";ERL:END
  130:
  140 PROCenquire
  150 PROCopen_file
  160 PROCdisplay_names
  170 PROCtidy_up
  180 END
  190:
  200 DEF PROCenquire
  210 PRINT '"GAZETTEER DISPLAY UTILITY ONE"'
  220 PRINT "This program displays all the areal unit names"
  230 PRINT " for a named area type from the Gazetteer file."'
  240 INPUT "Output file name : "out$
  250 INPUT "Areal unit type  : "type$
  260 ENDPROC
  270:
  280 DEF PROCopen_file
  290 gazhandle% = OPENIN( data_file$ )
  300 ENDPROC
  310:
  320 DEF PROCdisplay_names
  330 REM find the requested AU type record
  340 off%=0
  350 REPEAT
  360 PROCreadrec(off%)
  370 off% = off%+recsize%
  380 UNTIL FNmatch( $(rec%+3),type$ ) OR off% > maxtypes%*recsize%
  390:
  400IF off%>maxtypes%*recsize% THEN PRINT "AU Type not found":PROCtidy_up:END
  410:
  420 OSCLI "spool "+ out$
  430 REM display the areal unit type
  440 PRINT:FOR i=3 TO 32:VDU rec%?i:NEXT:PRINT:PRINT
  450:
  460 off%=rec%!36
  470 num_of_names% = rec%!34 AND &FFFF
  480:
  490 FOR i% = 1 TO num_of_names%
  500 PROCshowname(i%)
  510 NEXT i%
  520 OSCLI "spool"
  530 ENDPROC
  540:
  550 DEF FNmatch( S1$, S2$ )
  560 S3$=""
  570 FOR i% = 1 TO 32      : S3$ = S3$ + FNlower(MID$(S1$,i%,1)):NEXT i%
  580 FOR i% = 1 TO LEN(S2$): MID$(S2$,i%,1) = FNlower(MID$(S2$,i%,1)):NEXT i%
  590 IF INSTR( S3$,S2$,0 ) <> 0 = TRUE ELSE = FALSE
  600:
  610 DEF FNlower( ch$ )
  620 IF (ch$>="A")AND(ch$<="Z") THEN = CHR$( ASC(ch$)+ASC("a")-ASC("A") ) ELSE = ch$
  630:
  640 REM this procedure uses the fact that Nth sequential areal
  650 REM unit name within a block contains the Gazetteer offset
  660 REM to the name with number N (i.e. the first record has a
  670 REM pointer to name number 1, etc.)
  680:
  690 DEF PROCshowname(num%)
  700 PROCreadrec( off%+(num%-1)*recsize% )
  710 new_off% = rec%!42
  720 PROCreadrec( new_off% )
  730 IF hide_invalid_names% THEN IF (rec%!46 AND &FFFF) = 0 ENDPROC
  740 VDU9:PRINTnum%;:VDU9
  750 FOR i = 3 TO 32: VDU rec%?i : NEXT
  760 PRINT
  770 ENDPROC
  780:
  790 DEF PROCreadrec( offset% )
  800 PTR#gazhandle% = offset%
  810 FOR i = 0 TO recsize%-1
  820 rec%?i = BGET#gazhandle%
  830 NEXT
  840 ENDPROC
  850:
  860 DEF PROCtidy_up
  870 CLOSE#gazhandle%
  880 OSCLI "spool"
  890 ENDPROC
  900 
