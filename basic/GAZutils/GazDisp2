   10 REM > GazDisp2
   20 REM basic program to display areal unit names
   30 REM this version displays all the types and names
   40 REM available on the Domesday discs. (Hides invalid ones)
   50 REM Paul Cunnell (Logica) 18.9.87
   60 REM Code to spool to output file is REMed out
   70:
   80 recsize% = 48              :REM gazetteer record size (bytes)
   90 maxtypes% = 22             :REM limit for types to display
  100 hide_invalid_names% = TRUE :REM don't display names which have no
  110                            :REM valid MAPDATA record
  120 data_file$ = ":4.ads.r.Gazetteer" :REM where the gazetteer is
  130 DIM rec% recsize%
  140:
  150 ON ERROR PROCtidy_up : REPORT:PRINT" @ ";ERL:END
  160:
  170 PROCenquire
  180 PROCopen_file
  190 PROCdisplay_names
  200 PROCtidy_up
  210 END
  220:
  230 DEF PROCenquire
  240 PRINT '"GAZETTEER DISPLAY UTILITY TWO"'
  250 PRINT "This program displays all the areal unit names"
  260 PRINT " for all the area types in the Gazetteer file."'
  270:
  280 REM INPUT "Output file name ",out$
  290 ENDPROC
  300:
  310 DEF PROCopen_file
  320 gazhandle% = OPENIN(data_file$)
  330 ENDPROC
  340:
  350 DEF PROCdisplay_names
  360 off%=0
  370 REM OSCLI "SPOOL "+out$
  380 REPEAT
  390:
  400 PROCreadrec(off%)
  410 off% = off%+recsize%
  420 REM display the areal unit type
  430 PRINT:FOR i=3 TO 32:VDU rec%?i:NEXT:PRINT:PRINT
  440:
  450 off2%=rec%!36 : REM offset to this type's AU names
  460 num_of_names% = rec%!34 AND &FFFF
  470:
  480 REM display the areal unit names for the given type
  490 FOR i% = 1 TO num_of_names%
  500 PROCshowname(i%,off2%)
  510 NEXT i%
  520:
  530 UNTIL off% > (maxtypes%-1)*recsize%
  540 REM OSCLI "spool"
  550 ENDPROC
  560:
  570 REM this procedure uses the fact that Nth sequential areal
  580 REM unit name within a block contains the Gazetteer offset
  590 REM to the name with number N (i.e. the first record has a
  600 REM pointer to name number 1, etc.)
  610:
  620 DEF PROCshowname(num%,offset%)
  630 PROCreadrec( offset%+(num%-1)*recsize% )
  640 new_off% = rec%!42
  650 PROCreadrec( new_off% )
  660 IF hide_invalid_names% THEN IF (rec%!46 AND &FFFF) = 0 ENDPROC
  670:
  680 REM display the name
  690 VDU9: PRINT num%;: VDU9
  700 FOR i = 3 TO 32: VDU rec%?i : NEXT
  710 PRINT
  720 ENDPROC
  730:
  740 DEF PROCreadrec( offset% )
  750 PTR#gazhandle% = offset%
  760 FOR i = 0 TO recsize%-1
  770 rec%?i = BGET#gazhandle%
  780 NEXT
  790 ENDPROC
  800:
  810 DEF PROCtidy_up
  820 CLOSE#0
  830 REM OSCLI "spool"
  840 ENDPROC
